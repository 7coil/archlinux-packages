# $Id$
# Maintainer: Bash Booster <Booster.sdk@gmail.com>
# Based on original PKGBUILD by: Jan de Groot <jgc@archlinux.org>,  Alexander Baldeck <alexander@archlinux.org> and AUR contributors:
# Contributor: Marcin Kocur <marcin2006@gmail.com>
# Contributor: Riccardo Berto <riccardobrt@gmail.com>
# Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>
# Contributor: Dan Ziemba <zman0900@gmail.com>

pkgname=xf86-video-ati-git
pkgver=18.1.0.r22.gb1c01698
pkgrel=1
epoch=1
pkgdesc="X.org ati video driver"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libsystemd' 'mesa')
makedepends=('xorg-server-devel' 'systemd' 'X-ABI-VIDEODRV_VERSION=24.0')
conflicts=('xorg-server<1.20.0' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25' 'xf86-video-ati')
groups=('xorg-drivers')
source=('xf86-video-ati::git://anongit.freedesktop.org/xorg/driver/xf86-video-ati')
md5sums=('SKIP')

prepare() {
	
  patch --verbose -s -p0 < ../xf86-video-ati.patch
}

pkgver() {
  cd "$srcdir/xf86-video-ati"
  ( set -o pipefail
  git describe --long | sed 's/^xf86-video-ati-//;s/\([^-]*-g\)/r\1/;s/-/./g'
  )
}


build() {
  cd "$srcdir/xf86-video-ati"

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./autogen.sh --prefix=/usr \
    --enable-glamor
  make
}

check() {
  cd "$srcdir/xf86-video-ati"
  make check
}

package() {
  cd "$srcdir/xf86-video-ati"

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
